/*isCapstatValidationSuccess - This map Gets Validates and Assert CapabilityStatement validation passed succesfuly (done on the correct file & did not raise any errors)*/
(
  /*Write initial map status for UI to status file*/
    {
      'statusCode':'in-progress'
      ,'statusText':'in-progress'
    } ~> $writeFile('actionStatus_isCapstatValidationSuccess.json')

  /*Extract the CapStat from the HTTP response and write to a new file*/
    ;$CapabilityStatement := $readFile('getCapabilityStatement.json').data
    
    ;$writeFile($CapabilityStatement,'CapabilityStatement.json')
  
  /*Get and Validate Capstat*/
    ;$ValidationResultsCapabilityStatement := $validate($CapabilityStatement)
    ;$writeFile($ValidationResultsCapabilityStatement,'ValidationResults-CapabilityStatement.json')

  /*Perform test*/
    ;$testResult := (
      /*Check we are evluating a file that looks like the validation results of CapStat*/
        ($ValidationResultsCapabilityStatement.extension[url='http://hl7.org/fhir/StructureDefinition/operationoutcome-file'].valueString~>$contains('[CapabilityStatement]_['))
      and
      /*check there are no errors*/
        $not('error' in $ValidationResultsCapabilityStatement.issue.severity)
      )
  
  /*Prep pass & fail msg*/
    ;$msgPassed :={
      'statusCode':'passed'
      ,'statusText':'Passed'
    }
  
    ;$msgFailed := {
      'statusCode':'failed'
      ,'statusText':'Failed'
    }

  /*Write pass/fail msg based on test results*/
    ;$writeFile(($testResult ? $msgPassed : $msgFailed),'actionStatus_isCapstatValidationSuccess.json')
)